::
import {include, last} from "panda-parchment"

_preprocess = {}

_preprocess.readySignatureTable = (data) ->
  return false if !data
  head: ["name", "type", "description"]
  body: do ->
    for name, {type, description, isPromised} of data
      type = "ðŸ¤ž #{type}" if isPromised
      [name, type, description]

# Aliased for now, but may format differently in the future.
_preprocess.readyPropertiesTable = _preprocess.readySignatureTable

_preprocess.formatSignatureInputs = (inputs) ->
  string = ""
  for input, index in inputs
    if (last input) == "&"
      string += "[ #{input[...-1]}"
      if index == inputs.length - 1
        string += " ]"
      else
        string += " ,] "
    else
      string += input
      if index != inputs.length - 1
        string += " , "
  string

_preprocess.readySignatures = (title, signatures) ->
  for signature in signatures
    include signature,
      title: title
      inputs: _preprocess.formatSignatureInputs signature.inputs

  signatures

_preprocess.readyExamples = (examples) ->
  for example in examples
    example.title = "Example: #{example.title ? ""}"
    example.slug = "##{_slugify example.title}"
    example
::
